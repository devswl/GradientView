apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion androidCompileSdkVersion
    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    // KOTLIN
    implementation kotlinStdlib
}

//region Publish to jCenter
def libGroupId = 'com.mikhaellopez'
def libArtifactId = 'gradientview'
def libVersion = androidVersionName
def libDesc = 'Create gradient view in Android in the simplest way possible'
def libLicenses = ['Apache-2.0']
def libSiteUrl = 'https://github.com/lopspower/GradientView '
def libVCSUrl = 'https://github.com/lopspower/GradientView.git'

// Generate Source Jar
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Create the publication with the pom configuration
publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId libGroupId
            artifactId libArtifactId
            version libVersion

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // Add KotlinStdlib
                def kotlinStdlibDepNode = dependenciesNode.appendNode('dependency')
                kotlinStdlibDepNode.appendNode('groupId', kotlinStdlibGroupId)
                kotlinStdlibDepNode.appendNode('artifactId', kotlinStdlibArtifactId)
                kotlinStdlibDepNode.appendNode('version', kotlinStdlibVersion)
            }

            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/$libArtifactId-release.aar")
        }
    }
}
//endregion
